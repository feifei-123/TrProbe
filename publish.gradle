
apply plugin: 'maven'
apply from: rootProject.file('version.gradle')

afterEvaluate { project ->

    uploadArchives {
        repositories {
            mavenDeployer {
                //本地Maven地址
//                repository(url: "file:///Users/feifei/Desktop/TM/LocalMaven") }
                //私有maven 库地址
                repository(url: project.ext.MAVEN_URL) {
//                    authentication(userName: NEXUS_USERNAME, password: NEXUS_PASSWORD)
                }

                pom.project {
                    groupId project.ext.GROUP //唯一标识（通常为模块包名)
                    artifactId project.getName() // 项目名称（通常为类库模块名称)
                    version project.ext.VERSION_NAME //版本号
                    packaging 'aar'

                    licenses {
                        license {
                            name('The MIT License')
                            url 'https://raw.githubusercontent.com/hammercui/android-advance-cn/master/LICENSE'
                            distribution 'repo'
                        }
                    }

                    developers {
                        developer {
                            id project.ext.DEVEOPER_ID
                            name project.ext.DEVELOPER_NAME
                            email project.ext.DEVELOPER_EMAIL
                        }
                    }
                }

                pom.whenConfigured { pom ->
                    pom.dependencies.forEach { dep ->
                        if (dep.getVersion() == "unspecified") {
                            println("--modify the dependenies module in pom.xml--->>" + dep.getArtifactId())
                            dep.setGroupId(project.ext.GROUP)
                            dep.setVersion(project.ext.VERSION_NAME)
                        }
                    }
                }
            }
        }
    }

    task sourcesJar(type: Jar) {
        archiveClassifier = 'sources'
        from android.sourceSets.main.java.srcDirs
    }

    artifacts {
        archives sourcesJar
    }
}